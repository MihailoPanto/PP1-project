package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	boolean errorDetected;
	
	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }


:}

init with {:
	errorDetected = false;
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

// - TERMINALI -

terminal PROG, CONST, IF, NEW, PRINT, READ, RETURN, VOID, NAMESPACE;
terminal PLUS, MINUS, MUL, DIV, MOD, EQUAL_EQUAL, DIFFERENT, GREATER;
terminal GREATER_EQUAL, LESS, LESS_EQUAL, AND, OR, EQUAL, PLUS_PLUS;
terminal MINUS_MINUS, SEMI, DOUBLE_DOT, COMMA, DOT, LPAREN, RPAREN;
terminal LSBRACE, RSBRACE, LBRACE, RBRACE, TWO_DOUBLE_DOT, ELSE;
terminal String BOOL;
terminal Integer NUMBER ;
terminal String IDENT ;
terminal String CHAR;

// - NETERMINALI -

// program
// consts & var
nonterminal ConstVarDeclList, ConstVarDecl, ConstDecl, ConstDeclList, VarDecl, VarDeclList, VarDeclaration, OptionalSquareBrackets;	
// namespace
nonterminal NamespaceList;
// method
nonterminal MethodDeclList, MethodType, FormParsOpt, FormPars, FormPar, LocalVarDeclList;
// statement
nonterminal StatementList, Statement, OptPrint;
// designator
// operators
nonterminal MulOp, AddOp, AssignOp, RelOp;
// conditions
nonterminal ConditionList, CondTermList, CondFact;

// symboltable obj
nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program, Namespace, NamespaceName, MethodDecl, MethodTypeName, NamespaceAccess, Designator, DesignatorStatement;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ConstDeclaration;
// symboltable struct
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, ConstValue, Expr, Term, Factor;


// -------------------- PROGRAM --------------------

Program ::= (Program) PROG ProgName:p NamespaceList ConstVarDeclList LBRACE MethodDeclList RBRACE;

ProgName ::= (ProgName) IDENT:progName;

// -------------------- NAMESPACE --------------------

NamespaceList ::= (NameSpacesList) NamespaceList Namespace
		|
		(NoNameSpacesList) /*epsilon*/
		;

Namespace ::= (Namespace) NAMESPACE NamespaceName:n LBRACE ConstVarDeclList LBRACE RBRACE MethodDeclList RBRACE;

NamespaceName ::= (NamespaceName) IDENT:namespaceName;

// -------------------- METHOD --------------------

MethodDeclList ::= (MethodDeclarationList) MethodDeclList MethodDecl
		|
		(NoMethodDeclarationList) /*epsilon*/
		;
				
MethodDecl ::= (MethodDecl) MethodTypeName:methodTypeName LPAREN FormParsOpt RPAREN LocalVarDeclList LBRACE StatementList RBRACE;

MethodTypeName ::= (MethodTypeNameType) Type:methodType IDENT:methodName
		|
		(MethodTypeNameVoid) VOID:methodType IDENT:methodName
		;
			
FormParsOpt ::= (FormParsOptional) FormPars
		|
		(NoFormParsOptional)/*epsilon*/
		;
			
FormPars ::= (FPars) FormPars COMMA FormPar
		|
		(FPar) FormPar
		;

FormPar ::= (FormPar) Type IDENT OptionalSquareBrackets;

LocalVarDeclList ::= (LocalVarDeclarationList) LocalVarDeclList VarDecl
		|
		(NoLocalVarDeclarationList) /*epsilon*/
		;

// -------------------- CONST VAR --------------------

ConstVarDeclList ::= (ConstVarDeclarationList) ConstVarDeclList ConstVarDecl
		|
		(NoConstVarDeclarationList) /*epsilon*/
		;

ConstVarDecl ::= (ConstCVDeclaration) ConstDecl
		|
		(VarCVDeclaration) VarDecl
		;
				
				
// -------------------- CONST --------------------
				
ConstDecl ::= (TypeSemiConstDecl) CONST Type ConstDeclList SEMI;

		
ConstDeclList ::= (CommaConstDeclList) ConstDeclList COMMA ConstDeclaration
		|
		(SingleConstDecl) ConstDeclaration
		;
				
ConstDeclaration ::= (ConstDeclaration) IDENT:constName EQUAL ConstValue:constValue;

ConstValue ::= (NumberConst) NUMBER
		|
		(CharConst) CHAR
		|
		(BoolConst) BOOL
		;

// -------------------- VAR --------------------

VarDecl ::= (TypeSemiVarDecl) Type VarDeclList SEMI
		|
		(ErrorVarDecl) error SEMI:l
		{: parser.report_error("Oporavak izvrsen do ; u liniji " + lleft, null); :}
		;

VarDeclList ::= (CommaVarDeclList) VarDeclList COMMA VarDeclaration
		|
		(NoCommaVarDeclList) VarDeclaration
		|
		(ErrorCommaVList) error:l 
		{: parser.report_error("Oporavak izvrsen do , u liniji " + lleft, null); :}
		;
	
VarDeclaration ::= (VArrayDeclaration) IDENT:varName LSBRACE RSBRACE
		|
		(VDeclaration) IDENT:varName
		;
						
Type ::= (Type) IDENT:typeName;

// -------------------- STATEMENT --------------------

StatementList ::= (StmtList) StatementList Statement
		|
		(NoStmtList) /*epsilon*/
		;

Statement ::= (StmtDesignatorStatement) DesignatorStatement SEMI
		|
		(StmtError) error SEMI:l 
		{: parser.report_error("Oporavak izvrsen do , u liniji " + lleft, null); :}
		|
		(StmtRead) READ LPAREN Designator RPAREN SEMI
		|
		(StmtPrint) PRINT LPAREN Expr OptPrint RPAREN SEMI
		;

OptPrint ::= (CommaOptPrint) COMMA NUMBER
		|
		(NoOptPrint) /*epsilon*/
		;

DesignatorStatement ::= (DesignatorAssign) Designator AssignOp Expr
		|
		(DesignatorPlusPlus)Designator PLUS_PLUS
		|
		(DesignatorMinusMinus) Designator MINUS_MINUS
		;

Expr ::= (ExprMinus) MINUS Term
		|
		(ExprAddop) Expr AddOp Term
		|
		(ExprTerm) Term
		;

Term ::= (TermMulOp) Term MulOp Factor
		|
		(TermFactor) Factor
		; 

Factor ::= (FactorConstValueNum) NUMBER
		|
		(FactorConstValueChar) CHAR
		|
		(FactorConstValueBool) BOOL
		|
		(FactorNew) NEW Type LSBRACE Expr RSBRACE
		|
		(FactorExpr) LPAREN Expr RPAREN
		|
		(FactorDesignator) Designator
		;

Designator ::= (DesignatorIdentBraces) IDENT:desigName LSBRACE Expr RSBRACE
		|
		(DesignatorNamespaceBraces) IDENT:namespaceName TWO_DOUBLE_DOT IDENT:desigName LSBRACE Expr RSBRACE
		|
		(DesignatorNamespace) IDENT:namespaceName TWO_DOUBLE_DOT IDENT:desigName
		|
		(DesignatorIdent) IDENT:desigName
		;
	
// -------------------- CONDITIONS --------------------
			
ConditionList ::= (CondListOr) ConditionList OR CondTermList
		|
		(CondListNoOr)CondTermList
		;
		
CondTermList ::= (CondTermListAnd) CondTermList AND CondFact
		|
		(CondTermListNoAnd) CondFact
		;
		
CondFact ::= (CondFactRelOp) Expr RelOp Expr
		|
		(CondFactNoRelOp) Expr
		;			

// -------------------- OPERATORS --------------------

RelOp ::= (RelOp_EqualEqual) EQUAL_EQUAL 
		|	
		(RelOp_Different) DIFFERENT
		|	
		(RelOp_Greater) GREATER
		|	
		(RelOp_Greater_Equal) GREATER_EQUAL
		|	
		(RelOp_Less) LESS
		|	
		(RelOp_Less_Equal) LESS_EQUAL
		;

AssignOp ::= (AssignOpEqual) EQUAL;

AddOp ::= (AddOpPlus) PLUS
		|
		(AddOpMinus) MINUS
		;
		
MulOp ::= (Mul) MUL
		|
		(Div) DIV
		|
		(Mod) MOD
		;